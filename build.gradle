plugins {
    id "java"
    id "org.jetbrains.kotlin.jvm" version "1.5.31"
    id "application"
    id "com.diffplug.spotless" version "5.15.0"
}

group "co.moelten"
version "0.7.3-SNAPSHOT"

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.5.31"
    testImplementation group: "junit", name: "junit", version: "4.12"

    implementation(project(":ynab-api"))

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.2"
    implementation 'com.sksamuel.hoplite:hoplite-core:1.4.16'
    implementation 'com.sksamuel.hoplite:hoplite-yaml:1.4.16'
    implementation 'io.sentry:sentry:5.6.2'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
    testImplementation 'io.strikt:strikt-core:0.33.0'
    testImplementation 'io.mockk:mockk:1.12.2'
}

test {
    useJUnitPlatform()
}

application {
    mainClassName = "co.moelten.splity.SplityKt"
}

compileKotlin {
    kotlinOptions.jvmTarget = "11"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "11"
}

spotless {
    format 'misc', {
        target '*.gradle', '*.md', '.gitignore'

        trimTrailingWhitespace()
        indentWithSpaces(2)
        endWithNewline()
    }
    kotlin {
        trimTrailingWhitespace()
        endWithNewline()
    }
}

task createProperties(dependsOn: processResources) {
    doLast {
        mkdir("$buildDir/resources/main")
        def file = new File("$buildDir/resources/main/version.properties")
        file.createNewFile()
        file.withWriter { w ->
            Properties p = new Properties()
            p['version'] = project.version.toString()
            p.store w, null
        }
    }
}

classes {
    dependsOn createProperties
}
